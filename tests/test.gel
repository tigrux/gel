
(require Gtk)
(require Gst)

((. Gtk init) [])
((. Gst init) [])

(def window
    (new (. Gtk Window)
        "title" "Gtk and GStreamer from Gel"
        "default-width" 320
    )
)

(def box
    (new (. Gtk VBox)
        "border-width" 12
    )
)

(def label
    (new (. Gtk Label)
        "label" "Select a file to play back"
    )
)

(def chooser-button
    (new (. Gtk FileChooserButton))
)

(def quit-button
    (new (. Gtk Button)
        "use-stock" TRUE
        "label" (. Gtk STOCK-QUIT)
    )
)

(def player ((. Gst ElementFactory make) "playbin2" "player"))

(connect chooser-button "file-set"
    (fn (w)
        (set chooser-button "sensitive" FALSE)
        (let (filename ((. chooser-button get-filename)))
            (set player "uri" (+ "file://" filename))
            ((. player set-state) (. Gst State playing))
        )
    )
)

(def player-bus ((. player get-bus)))
((. player-bus add-signal-watch))

(defn quit (send & args)
    ((. player set-state) (. Gst State null))
    ((. Gtk main-quit))
)

(connect player-bus "message::eos" quit)
(connect quit-button "clicked" quit)
(connect window "hide" quit)

((. window add) box)

(let (box-pack (. box pack-start))
    (for widget [label chooser-button quit-button]
        (box-pack widget TRUE TRUE 12)
    )
)

((. window show-all))

((. Gtk main))

