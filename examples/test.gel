
(require Gtk)
(require Gst)

((. Gtk init))
((. Gst init))

(define window
    (new (. Gtk Window)
        "title" "Gtk and GStreamer from Gel"
        "default-width" 320
    )
)

(define box
    (new (. Gtk VBox)
        "border-width" 12
    )
)

(define label
    (new (. Gtk Label)
        "label" "Select a file to play back"
    )
)

(define chooser-button
    (new (. Gtk FileChooserButton))
)

(define quit-button
    (new (. Gtk Button)
        "use-stock" TRUE
        "label" (. Gtk STOCK-QUIT)
    )
)

(define player ((. Gst ElementFactory make) "playbin2" "player"))

(let (bus ((. player get-bus)))
    ((. bus add-signal-watch))
    (connect bus "message::eos" (. Gtk main-quit))
)

(connect window "hide" (. Gtk main-quit))

(connect chooser-button "file-set"
    (function (w)
        (set chooser-button "sensitive" FALSE)
        (let (filename ((. chooser-button get-filename)))
            (set player "uri" (+ "file://" filename))
            ((. player set-state) (. Gst State playing))
        )
    )
)

(connect quit-button "clicked" (. Gtk main-quit))

((. window add) box)
((. box pack-start) label TRUE TRUE 12)
((. box pack-start) chooser-button FALSE FALSE 12)
((. box pack-start) quit-button FALSE FALSE 12)

((. window show-all))

((. Gtk main))

